plugins {
    id 'java'
    //root모듈 에서 build시 bootJar가 없다고 예외가 뜨기 때문에 해당 예외를 제거하기 위해 false 설정
    //플러그인을 준비만 하고, root모듈 에서는 사용하지 않음
    id 'org.springframework.boot' version '2.7.14' apply(false)
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

allprojects {
    group = 'hello'
    version = '0.0.1-SNAPSHOT'

    java {
        sourceCompatibility = '11'
    }

    repositories {
        mavenCentral()
    }
}

subprojects {
    //Java 프로젝트에 필요한 기본 설정들을 세팅
    apply { plugin('java') }
    //Spring boot 프로젝트에 필요한 기본 설정들을 세팅
    apply { plugin('org.springframework.boot') }
    //Spring boot에서 의존성 버전 관리를 세팅
    apply { plugin('io.spring.dependency-management') }

    //Application을 실행한 api 모듈에서만 bootJar가 필요하고, 나머지 모듈에서는 bootJar가 필요하지않음
    //bootJar : 일반적인 Jar랑 다르게 Spring에서만 제공해주는 Jar, 기본적으로 Gradle에서는 bootJar의 설정이 기본으로 True
    //Spring 의존성이 없는 Core 모듈에서는 bootJar를 찾지 못해서 에러가 난다.

    //bootJar 설정을 다 끈다.
//    tasks.named('bootJar'){
//        enabled = false
//    }

    //특정 모듈만 끄고, 나머지는 켠다
    if (project.name == 'core') {
        tasks.named('bootJar') {
            enabled = false
        }
    }

    //Jar 설정을 킨다
    tasks.named('jar'){
        enabled = true
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
